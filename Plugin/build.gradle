plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

dependencies {
    implementation project(':API')
    implementation project(':v1_15_R1')
    implementation project(':v1_16_R1')
    implementation project(':v1_16_R2')
    implementation project(':v1_16_R3')
    implementation 'org.apache.maven:maven-artifact:3.0.3'
    implementation 'org.jetbrains:annotations:19.0.0'
    implementation 'org.bstats:bstats-bukkit:1.7'
    compileOnly 'org.spigotmc:spigot-api:1.16.3-R0.1-SNAPSHOT'
    compileOnly 'commons-io:commons-io:2.8.0'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'com.willfp:EcoEnchants:5.3.1'
}

shadowJar {
    relocate('org.bstats.bukkit', 'com.willfp.illusioner.shaded.bstats')
    relocate('org.jetbrains', 'com.willfp.illusioner.shaded.jetbrains')
    relocate('org.intellij', 'com.willfp.illusioner.shaded.intellij')
    relocate('org.apache.maven', 'com.willfp.illusioner.shaded.maven')
    archiveFileName = findProperty("plugin-name") + " v" + findProperty("version") + ".jar"
}

processResources {
    filesNotMatching(["**/*.png", "**/models/**", "**/textures/**"]) {
        expand projectVersion: findProperty("version")
    }
}

jar {
    archiveFileName = findProperty("plugin-name") + " v" + findProperty("version") + " " + "unshaded" + ".jar"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Jar) {
    destinationDirectory = file("$rootDir/bin/")
}

build.dependsOn shadowJar

description = 'Illusioner'
compileJava.options.encoding = 'UTF-8'
